buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.8.+'
  }
}

apply plugin: 'android-library'
apply plugin: 'checkstyle'

dependencies {
  repositories {
    mavenCentral()
  }
  compile 'com.android.support:support-v4:19.0.+'
  compile 'org.easytesting:fest-assert-core:2.0M10'
}

android {
  compileSdkVersion 19
  buildToolsVersion '19.0.0'

  defaultConfig {
    minSdkVersion 7
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

checkstyle {
  configFile project.file('checkstyle.xml')
  showViolations true
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  project.tasks.getByName("check").dependsOn checkstyle

  // Only attach a jar for non-debug build types.
  if (!name.equals(com.android.builder.BuilderConstants.DEBUG)) {
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
  }
}

apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
